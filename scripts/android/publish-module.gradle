apply plugin: 'maven-publish'
apply plugin: 'signing'

def LIB_VERSION = System.getenv('PLUGIN_VERSION')
def PLUGIN_NAME = System.getenv('PLUGIN_NAME')

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = 'com.capacitorjs'
version = LIB_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Coordinates
                groupId 'com.capacitorjs'
                artifactId PLUGIN_NAME
                version LIB_VERSION

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                // POM Data
                pom {
                    name = PLUGIN_NAME
                    description = 'Capacitor Android ' + PLUGIN_NAME + ' plugin native library'
                    url = 'https://github.com/ionic-team/capacitor-plugins'
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://github.com/ionic-team/capacitor-plugins/blob/main/' + PLUGIN_NAME + '/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            name = 'Ionic'
                            email = 'hi@ionic.io'
                        }
                    }

                    // Version Control Info
                    scm {
                        connection = 'scm:git:github.com:ionic-team/capacitor-plugins.git'
                        developerConnection = 'scm:git:ssh://github.com:ionic-team/capacitor-plugins.git'
                        url = 'https://github.com/ionic-team/capacitor-plugins/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}